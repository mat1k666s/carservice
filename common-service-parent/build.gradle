plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.3' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'com.gorylenko.gradle-git-properties' version '2.4.1' apply false
}

group 'common-microservice-parrent'
version "${System.getenv('MICROSERVICE_VERSION') != null ? System.getenv('MICROSERVICE_VERSION') : '0.0.1'}"

ext {
    set('springCloudVersion', "2024.0.0")
    set('springDocVersion', "2.8.3")
    set('mapstructVersion', "1.6.2")
    set('lombokVersion', "1.18.36")
    set('lombokMapstructBindingVersion', "0.2.0")
    set('postgreVersion', "42.7.4")
    set('apachePoiVersion', "5.3.0")
    set('apacheCommonsVersion', "3.17.0")
}

allprojects {
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.gorylenko.gradle-git-properties'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${parent.springCloudVersion}"
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-web'

        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly "org.postgresql:postgresql:${parent.postgreVersion}"

        implementation 'io.micrometer:micrometer-core'
        implementation 'io.micrometer:context-propagation'
        implementation 'io.micrometer:micrometer-tracing'
        implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
        implementation 'io.micrometer:micrometer-tracing-bridge-brave'
        implementation 'io.micrometer:micrometer-registry-prometheus'

        implementation 'org.apache.logging.log4j:log4j-api'
        implementation 'org.apache.logging.log4j:log4j-core'
        runtimeOnly 'com.github.loki4j:loki-logback-appender:1.4.2'

        implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${parent.springDocVersion}"

        implementation "org.projectlombok:lombok:${parent.lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${parent.lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${parent.lombokVersion}"
        annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${parent.lombokMapstructBindingVersion}"

        implementation "org.mapstruct:mapstruct:${parent.mapstructVersion}"
        annotationProcessor "org.mapstruct:mapstruct-processor:${parent.mapstructVersion}"
        testAnnotationProcessor "org.mapstruct:mapstruct-processor:${parent.mapstructVersion}"

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.projectreactor:reactor-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        implementation "org.apache.poi:poi:${parent.apachePoiVersion}"
        implementation "org.apache.poi:poi-ooxml:${parent.apachePoiVersion}"
        implementation "org.apache.commons:commons-lang3:${parent.apacheCommonsVersion}"

        implementation "org.springframework.cloud:spring-cloud-starter-stream-kafka"
        implementation "org.springframework.cloud:spring-cloud-stream-binder-kafka"
        implementation "org.springframework.kafka:spring-kafka"
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.compilerArgs += ['-parameters']
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
    }
}